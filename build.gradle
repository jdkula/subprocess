import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.johnrengelman.shadow" version "2.0.2"
    id "org.jetbrains.dokka" version "0.9.16"
}

group = "pw.jonak"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile "junit:junit:4.12"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'html'
    jdkVersion = 8
    // includeNonPublic = true // Not working...
    impliedPlatforms = ["JVM"]
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/dokkadoc"
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

compileTestJava {
    options.encoding = "UTF-8"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = getSecret('BINTRAY_USER')
    key = getSecret('BINTRAY_API_KEY')
    publications = ['BintrayPublication']
    pkg {
        repo = 'subprocess'
        name = 'subprocess'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/jdkula/subprocess.git'
        version {
            name = "v$project.version"
            desc = "v$project.version"
            released = new Date()
            vcsTag = "v$project.version"
        }
    }
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().appendNode('packaging', 'jar')
            }
        }
    }
}

static def getSecret(String key) {
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    return props[key]
}